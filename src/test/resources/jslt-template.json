def is-type-mobile(x)
    $x.key == "type" and $x.value == "mobile"

let cpf = .user.cpf
let products = .user.products
let sign = .user.signature

{
   "entries":
   [for (.user.accounts)
      let key = .key
      let prdByAcct = [for ($products) . if ($key == .account)]
      let rolesByAcct = flatten([for ($products) .roles if ($key == .account)])
      {
         "id": $key,
         "_ref": $cpf,
         "attribs": {
            for (.) .key: .value if (.key != "key")
         },
         "pk": if ($sign.owner == .key) $sign.pub else null,
         "options":
            [for (.) "NOPASSWD" if ( is-type-mobile(.) )] +
            [for ($prdByAcct)
               "MANAGER" if (contains("admin", .roles))
            ] +
            [for ($rolesByAcct)
               uppercase(.) if (. != "admin" and . != "unused")
            ]
      }
   ]
}